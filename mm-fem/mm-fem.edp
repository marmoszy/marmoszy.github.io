// Free vibrations of 3.8cm x 1.41cm PIC255 cylindrical disk  
// Marek Moszynski 30.03.2020, 17.7.2024, 30.1.2026
// Run: freefem++ -wg -ns -v 0 mm-fem.edp

// ------ Frequency ------
real[int] ff=[126e3];         // frequency table
 
// ------ Geometry -------
real M=19, N=14;  // mesh dimensions
mesh Th = square(M, N, [0.019*x, 0.0141*y]);

// ------ PIC255  -------
real rho = 7800;  // density
complex  c11 = 122.907e9*(1+1i/58.6),   c12 = 76.6089e9*(1+1i/58.6),   // elastic
         c13 = 71.178e9*(1+1i/80),      c33 = 97.056e9*(1+1i/145),    c44 = 23.5e9*(1+1i/120),  
         e31 = -7.8417,  e33 = 13.5583, e15 = 12.244,                  // piezoelectric
         epsS11 = 8.234e-9*(1-1i/50),   epsS33 = 7.58e-9*(1-1i/50);    // dielectric
func C = [[c11, c12, c13,  0,    0,   e31],
          [c12, c11, c13,  0,    0,   e31],
          [c13, c13, c33,  0,    0,   e33],
          [0,    0,   0,  c44, e15,    0 ],
          [0,    0,   0,  e15,-epsS11,  0 ],
          [e31, e31, e33,  0,    0,-epsS33]];

// ------ Macros --------
macro L(u,v,p) [dx(u), u/x, dy(v), dy(u)+dx(v), dx(p), dy(p)]//

for(int ii=0; ii<ff.n; ii++) {   // for all frequencies
  real f0 = ff[ii], w = 2*pi*f0;

  // ------ Problem -------
  fespace Vh(Th,[P1,P1,P1]); // P1: piecewise linear FE
  Vh<complex> [ur,uz,uphi], [vr,vz,vphi];  // variational variables
  Vh [rur,ruz,ruphi], [rvr,rvz,rvphi];     // variational variables

  solve Piezo2D([ur,uz,uphi],[vr,vz,vphi]) 
  = - int2d(Th)(x*rho*w^2*(ur*vr+uz*vz))
    + int2d(Th)(x*L(vr,vz,vphi)'*C*L(ur,uz,uphi)) 
    + on(1,uphi=1)                         // bottom side
    + on(3,uphi=0)                         // top side
    + on(4,ur=0);                          // axis of symmetry

  // -------Plot ----------
  real c2 = 0.5e6;  // scaling coeff  // real(uphi);
  mesh Th2 = movemesh(Th,[x+c2*real(ur),y+c2*real(uz)]);     // deformed mesh
  plot(Th,Th2,uphi,cmm="f0 = " + f0/1e3 + " kHz ",fill=true, // value=true,
    //ps="mm-fem-edp_"+(f0/1e3)+"_"+M+"_"+N+".eps");        // display and save as eps
    pdf="mm-fem-edp_"+(f0/1e3)+"_"+M+"_"+N+".pdf");         // display and save as pdf
}